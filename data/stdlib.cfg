do = [ arg1 ]
loopconcat = [
    sum = "";
    loop $arg1 $arg2 [sum = (concat $sum (arg3))]
    result $sum;
]
macro = [
    $arg1 = (concat [format [@@arg2]] (loopconcat i $numargs [concatword " $arg" (+ $i 1)]))
]
bindvar = [
  bind $arg1 [@arg2 (= $@arg2 0); if (= $@arg2 0) [echo @@arg2 OFF] [ echo @@arg2 ON]]
]
editbindvar = [
  editbind $arg1 [@arg2 (= $@arg2 0); if (= $@arg2 0) [echo @@arg2 OFF] [ echo @@arg2 ON]]
]
bindmod = [
  bind $arg1 [@arg2 1; onrelease [@@arg2 0]]
]
editbindmod = [
  editbind $arg1 [@arg2 1; onrelease [@@arg2 0]]
]
quine = [ echo (format "quine = [%1]" $quine) ]
loadsky = [
    skybox $arg1
    if (> $numargs 1) [
        spinsky $arg2
    ] [
        if (!=f $spinsky 0) [spinsky 0]
    ]
]
mapmsg = [ maptitle $arg1 ]
